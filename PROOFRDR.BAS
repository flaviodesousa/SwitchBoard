10 'Automatic Proofreader Version 3.0 (Lines 205,206 added/190 deleted/470,490 changed from V2.0)
100 DIM L$(500),LNUM(500):COLOR 0, 7, 7:KEY OFF : CLS : MAX = 0 : LNUM(0) = 65536.0!
110 ON ERROR GOTO 120:KEY 15, CHR$(4) + CHR$(70): On KEY(15) GOSUB 640:KEY(15) On:GoTo 130
120 RESUME 130
130 DEF SEG=&H40:W = PEEK(&H4A)
140 ON ERROR GOTO 650:PRINT : PRINT"Proofreader Ready."
150 LINE INPUT L$:Y = CSRLIN - INT(LEN(L$) / W) - 1:LOCATE Y,1
160 DEF SEG=0:POKE 1050, 30: POKE 1052, 34: POKE 1054, 0: POKE 1055, 79: POKE 1056, 13: POKE 1057, 28:LINE INPUT L$:DEF SEG : If L$ = "" Then 150
170 IF LEFT$(L$,1)=" " THEN L$=MID$(L$,2):GoTo 170
180 IF VAL(LEFT$(L$,2))=0 And MID$(L$,3,1)=" " THEN L$=MID$(L$,4)
200 IF ASC(L$)>57 THEN 260 'no line number, therefore command
205 BL=INSTR(L$," "):If BL = 0 Then BL$=L$:GoTo 206 Else BL$=LEFT$(L$,BL-1)
206 LNUM=VAL(BL$):TEXT$ = MID$(L$, LEN(STR$(LNUM)) + 1)
210 IF TEXT$="" THEN GOSUB 540:If LNUM = LNUM(P) Then Gosub 560:GoTo 150 Else 150
220 CKSUM=0:For I = 1 To LEN(L$) : CKSUM = (CKSUM + ASC(MID$(L$, I)) * I) And 255 : Next:LOCATE Y,1:PRINT CHR$(65 + CKSUM / 16)+CHR$(65+(CKSUM And 15))+" "+L$
230 GOSUB 540:If LNUM(P) = LNUM Then L$(P)=TEXT$:GoTo 150 'replace line
240 GOSUB 580:GoTo 150 'insert the line
260 TEXT$="":For I = 1 To LEN(L$) : A = ASC(MID$(L$, I)) : TEXT$ = TEXT$ + CHR$(A + 32 * (A > 96 And A < 123)) : Next
270 DELIMITER=INSTR(TEXT$," "):COMMAND$ = TEXT$ : ARG$ = "" : If DELIMITER Then COMMAND$=LEFT$(TEXT$,DELIMITER-1):ARG$ = MID$(TEXT$, DELIMITER + 1) Else DELIMITER=INSTR(TEXT$,CHR$(34)):If DELIMITER Then COMMAND$=LEFT$(TEXT$,DELIMITER-1):ARG$ = MID$(TEXT$, DELIMITER)
280 IF COMMAND$<>"LIST" THEN 410
290 OPEN "scrn:" FOR OUTPUT AS #1
300 IF ARG$="" THEN FIRST=0:P = MAX - 1 : GoTo 340
310 DELIMITER=INSTR(ARG$,"-"):If DELIMITER = 0 Then LNUM=VAL(ARG$):GOSUB 540:FIRST = P : GoTo 340
320 FIRST=VAL(LEFT$(ARG$,DELIMITER)):LAST = VAL(MID$(ARG$, DELIMITER + 1))
330 LNUM=FIRST:GOSUB 540:FIRST = P : LNUM = LAST : GOSUB 540:If P = 0 Then P=MAX-1
340 FOR X=FIRST TO P:N$ = MID$(STR$(LNUM(X)), 2) + " "
350 IF CKFLAG=0 THEN A$="":GoTo 370
360 CKSUM=0:A$ = N$ + L$(X) : For I = 1 To LEN(A$) : CKSUM = (CKSUM + ASC(MID$(A$, I)) * I) And 255 : Next : A$ = CHR$(65 + CKSUM / 16) + CHR$(65 + (CKSUM And 15)) + " "
370 PRINT #1,A$+N$+L$(X)
380 IF INKEY$<>"" THEN X=P
390 NEXT :CLOSE #1:CKFLAG = 0
400 GOTO 130
410 IF COMMAND$="LLIST" THEN OPEN "lpt1:" FOR OUTPUT AS #1:GoTo 300
420 IF COMMAND$="CHECK" THEN CKFLAG=1:GoTo 290
430 IF COMMAND$<>"SAVE" THEN 450
440 GOSUB 600:OPEN ARG$ For OUTPUT As #1:ARG$ = "" : GoTo 300
450 IF COMMAND$<>"LOAD" THEN 490
460 GOSUB 600:OPEN ARG$ For INPUT As #1:MAX = 0 : P = 0
470 WHILE Not EOF(1):LINE INPUT #1,L$:BL = INSTR(L$, " ") : BL$ = LEFT$(L$, BL - 1) : LNUM(P) = VAL(BL$) : L$(P) = MID$(L$, LEN(STR$(VAL(BL$))) + 1) : P = P + 1 :WEND
480 MAX=P:CLOSE #1:GoTo 130
490 IF COMMAND$="NEW" THEN INPUT "Erase program - Are you sure";L$:If LEFT$(L$, l) = "y" Or LEFT$(L$, 1) = "Y" Then MAX=0:LNUM(0) = 65536.0! : GoTo 130 : Else 130
500 IF COMMAND$="BASIC" THEN COLOR 7,0,0:On Error GoTo 0 : CLS : End
510 IF COMMAND$<>"FILES" THEN 520
515 IF ARG$="" THEN ARG$="A:" ELSE SEL=1:GOSUB 600
517 FILES ARG$:GoTo 130
520 PRINT"Syntax error":GoTo 130
540 P=0:While LNUM > LNUM(P) And P < MAX : P = P + 1 :WEND:Return
560 MAX=MAX-1:For X = P To MAX : LNUM(X) = LNUM(X + 1) : L$(X) = L$(X + 1) : Next : Return
580 MAX=MAX+1:For X = MAX To P + 1 Step -1 : LNUM(X) = LNUM(X - 1) : L$(X) = L$(X - 1) : Next : L$(P) = TEXT$ : LNUM(P) = LNUM : Return
600 IF LEFT$(ARG$,1)<>CHR$(34) THEN 520 ELSE ARG$=MID$(ARG$,2)
610 IF RIGHT$(ARG$,1)=CHR$(34) THEN ARG$=LEFT$(ARG$,LEN(ARG$)-1)
620 IF SEL=0 And INSTR(ARG$,".")=0 THEN ARG$=ARG$+".BAS"
630 SEL=0:Return
640 CLOSE #1:CKFLAG = 0 : PRINT"Stopped.": Return 150
650 PRINT"Error #";ERR:Resume 150